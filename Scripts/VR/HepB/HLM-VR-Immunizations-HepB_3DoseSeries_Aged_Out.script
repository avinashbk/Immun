(* 

HELPER SUITE INFO: 
# https: //github.cerner.com/EggPlant/IPDev-mPages-Shared-Immunizations
# https: //github.cerner.com/EggPlant/IPDev-Shared-Millennium 
# https: //github.cerner.com/EggPlant/IPDev-Millennium-Shared-HealthMaintenanceTools
#https://github.cerner.com/EggPlant/IPDev-Shared-Utilities

RQM TEST CASE LINK: 
# https://jazz.cerner.com:9443/qm/web/console/IP#action=com.ibm.rqm.planning.home.actionDispatcher&subAction=viewTestCase&id=116482

*)
params domain

Log "Test running in domain" && domain
set domain to "Immunizations/Util/setup".domainrun(domain)

set setup to "UTIL_Json".initializeResourceData(JSONValue(file resourcePath("setup.json")),domain)
set username to setup.username
set password to decodeText(setup.("password").trimAll, "HLM")
set encounterOrg to setup.encounterOrg
set encounterType to setup.encounterType
set conversation to setup.conversation
set the remoteWorkInterval to value(setup.("remoteWorkInterval"))
set relationship to setup.relationship

set common to JSONValue(file resourcePath("Common.json"))
set menu to common.menu
set subMenu to common.subMenu
set doubleDashImage to common.doubleDashImage
set HepBVaccineStrikeThroughImg to common.HepBVaccineStrikeThroughImg

set textStrings to "UTIL_Json".initializeResourceData(JSONValue(file resourcePath("textStrings.json")),domain)
set agedOut to textStrings.agedOut
set viewForecast to textStrings.viewForecast

set testData to JSONValue(file ResourcePath("VR/HepB_3_DoseSeries/testData.json")).(value of domain)
set lastName to testData.Patient_Aged_Out.lastName
set firstName to testData.Patient_Aged_Out.firstName
set patientName to testData.Patient_Aged_Out.nameFullFormatted
set newLastName to testData.Patient_Aged_Out.newLastName
set sex to testData.Patient_Aged_Out.sex
set dob to formattedTime("%m/%d/%Y", value(testData.Patient_Aged_Out.dob))
set vaccineName1 to testData.vaccineName1



try
	---------------------------------------------------------------------------------------------------------------
	# PATIENT SETUP
	Log "PATIENT CREATION: Open PmLaunch and Create patient(s) for the test"
	run "Immunizations/PatientCreation".openPMLaunch username, password, domain
	run "Immunizations/PatientCreation".openConversationContinuous conversation
	run "Immunizations/PatientCreation".searchPatient lastName, firstName
	run "Immunizations/PatientCreation".patientSearched encounterOrg, firstName, sex, "%m/%d/%Y", dob, encounterType
	run "Util".closeApplication 2
	
	Try
		#################################################################################
		# TEST STEPS
		
		BeginTestCase "VR/HepB/HLM-VR-Immunizations-HepB_3DoseSeries_Aged_Out"
		
		Log "Util".stepNumber & "ACTION: Open Patient 3's chart, navigate to the immunization component and review the MPage."
		run "Immunizations/setup".loginAndLoadcomponentViaMenu username, password, domain, patientName, menu, subMenu,  relationship
		
		Log "VERIFICATION: Ensure Hepatitis B displays in the historical section with a status of 'Aged Out'."
		run "Immunizations/WorkflowMPage/Table/Validations".verifyStatusColumn vaccineName1, agedOut
		run "Immunizations/WorkflowMPage/Table/Validations".verifyVaccineNameHistorySection vaccineName1
		
		Log "VERIFICATION: Ensure the Administrations Column displays with '--'."
		run "Immunizations/WorkflowMPage/Table/Validations".verifyAdministrationColumn vaccineName1, doubleDashImage, false
		
		Log "VERIFICATION: Ensure the Next Recommneded Column displays with '--'."
		run "Immunizations/WorkflowMPage/Table/Validations".verifyNextRecommendedColumn vaccineName1, doubleDashImage
		
		Log "VERIFICATION: Ensure Last action displays with '--'."
		run "Immunizations/WorkflowMPage/Table/Validations".verifyLastActionColumn vaccineName1, doubleDashImage
		
		Log "VERIFICATION: Ensure Last action Date displays with '--'."
		run "Immunizations/WorkflowMPage/Table/Validations".verifyLastActionDateColumn vaccineName1, doubleDashImage
		captureScreen(Name: "Step" & Global stepNumber & "- Aged out MPage View P3")
		---------------------------------------------------------------------------------------------------------------
		
		Log "Util".stepNumber & "ACTION: Click on Hepatitis B."
		run "Immunizations/WorkflowMPage/Table/Actions".clickVaccineGroupName vaccineName1
		
		Log "VERIFICATION: Ensure in the header of the side panel, Hepatitis B displays with a status of 'Aged Out'."
		set sidePanelHeaderRectangle to "Immunizations/WorkflowMPage/SidePanel/Helpers".sidePanelTopHeaderRectangle()
		run "Immunizations/Util/Validation".verifyText vaccineName1, sidePanelHeaderRectangle
		run "Immunizations/WorkflowMPage/SidePanel/Validations".verifyValidNextRecommendedDate agedOut, sidePanelHeaderRectangle
		captureScreen(Name: "Step" & Global stepNumber & "- Dose1 Aged out side panel P3")
		----------------------------------------------------------------------------------------------------------------
		
		Log "Util".stepNumber & "ACTION: Click on the View Forecast button."
		run "Immunizations/WorkflowMPage/Table/Actions".clickViewForecast viewForecast
		
		Log "VERIFICATION: Ensure Hepatitis B displays with a strikethrough and a status of 'Aged Out'."
		set HepBVaccineStrikeThroughImgRowSearchRectangle to "Immunizations/Forecaster-Forecast/Helpers".rowSearchRectangleImage(HepBVaccineStrikeThroughImg)
		set firstColumnSearchRectangle to "Immunizations/Forecaster-Common/Helpers".firstcolumnSearchRectangle()
		run "Immunizations/Util/Validation".verifyText agedOut, "Immunizations/WorkflowMPage/Table/Helpers".cellIntersection(HepBVaccineStrikeThroughImgRowSearchRectangle, firstColumnSearchRectangle)
		captureScreen(Name: "Step" & Global stepNumber & "- Aged out Forecast View P3")
		---------------------------------------------------------------------------------------------------------------
		
		Log "Util".stepNumber & "ACTION: Click on Hepatitis B."
		click ImageLocation("Immunizations/Forecaster-Common/HepBStrikethrough")
		
		Log "VERIFICATION: In the right hand pane, ensure 'Aged Out - Hepatitis B' displays in the top section."
		set rightPaneHeaderSearchRectangle to "Immunizations/Forecaster-Forecast/Helpers".expandedViewRightPaneHeaderSearchRectangle
		run "Immunizations/Util/Validation".verifyText agedOut && "-" && vaccineName1, rightPaneHeaderSearchRectangle
		captureScreen(Name: "Step" & Global stepNumber & "- Aged out Expanded View P3")
		---------------------------------------------------------------------------------------------------------------
		
		Log "Util".stepNumber & "ACTION: Click out of the expanded view and click on the List View."
		Click text: "Print", searchrectangle: "Immunizations/Common/Helpers".tabListSearchRectangle
		run "Immunizations/Forecaster-Common/Actions".clickFlipViews
		
		Log "VERIFICATION: Ensure 'No vaccine history to display' is shown in the body of the list view."
		run "Immunizations/Forecaster-List/Validations".verifyNoVaccineHistory
		captureScreen(Name: "Step" & Global stepNumber & "- Aged out List View P3")
		---------------------------------------------------------------------------------------------------------------
		
		Log "Util".stepNumber & "ACTION: Close patient 3's chart."
		run "Immunizations/Setup".closeApplication 
		---------------------------------------------------------------------------------------------------------------
		
		EndTestCase "VR/HepB/HLM-VR-Immunizations-HepB_3DoseSeries_Aged_Out"
		
	Catch
		LogError "Patient Workflow Steps Exception encountered: " && the exception.reason
		captureScreen "PWF_Ex" & formattedTime("_%m%d%Y%H%M%S", the time)
	End Try
	
	#################################################################################
	# PATIENT CLEANUP
	Log "PATIENT CLEANUP: Open PmLaunch and Rename patient(s) from the test"
	run "Immunizations/PatientCreation".openPMLaunch username, password, domain
	run "Immunizations/PatientCreation".openConversationContinuous conversation
	run "Immunizations/PatientCreation".searchPatient lastName, firstName
	run "Immunizations/PatientCreation".renamePatientSearched newLastName
	run "Util".closeApplication 2
	---------------------------------------------------------------------------------------------------------------
	
Catch
	LogError "Exception encountered: " && the exception.reason
	captureScreen "Ex" & formattedTime("_%m%d%Y%H%M%S", the time)
End Try
---------------------------------------------------------------------------------------------------------------

# SUT CLEANUP
run "Cleanup".ZeroStateCloseAllApps
